# Find BWAPI (C++ library https://github.com/bwapi/bwapi/releases)
#  Input variable: BWAPI_PATH
#  Output variables:
#    BWAPI_INCLUDE_DIRS
#    BWAPI_LIB_DEBUG // BWAPId.lib
#    BWAPI_LIB_RELEASE // BWAPI.lib
#    BWAPI_DLL_DEBUG // BWAPId.dll
#    BWAPI_DLL_RELEASE // BWAPI.dll

find_path(BWAPI_INCLUDE_DIRS NAMES BWAPI.h PATHS ${BWAPI_PATH}/include)

set(old_prefixes ${CMAKE_FIND_LIBRARY_PREFIXES})
set(old_suffixes ${CMAKE_FIND_LIBRARY_SUFFIXES})
set(CMAKE_FIND_LIBRARY_PREFIXES "")

set(CMAKE_FIND_LIBRARY_SUFFIXES ".lib")
find_library(BWAPI_LIB_DEBUG NAMES BWAPId PATHS ${BWAPI_PATH}/lib)
find_library(BWAPI_LIB_RELEASE NAMES BWAPI PATHS ${BWAPI_PATH}/lib)
set(BWAPI_LIBRARIES ${BWAPI_LIB_DEBUG} ${BWAPI_LIB_RELEASE})

set(CMAKE_FIND_LIBRARY_SUFFIXES ".dll")
find_library(BWAPI_DLL_DEBUG NAMES BWAPId PATHS ${BWAPI_PATH}/Starcraft/bwapi-data/)
find_library(BWAPI_DLL_RELEASE NAMES BWAPI PATHS ${BWAPI_PATH}/Starcraft/bwapi-data/)
set(BWAPI_DLLS ${BWAPI_DLL_DEBUG} ${BWAPI_DLL_RELEASE})

set(CMAKE_FIND_LIBRARY_PREFIXES ${old_prefixes})
set(CMAKE_FIND_LIBRARY_SUFFIXES ${old_suffixes})

include(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(BWAPI DEFAULT_MSG BWAPI_LIBRARIES BWAPI_DLLS BWAPI_INCLUDE_DIRS)
